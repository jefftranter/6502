E000R
SCR
HIMEM=7000
10 REM HOROSCOPE GENERATOR
20 REM BY JEFF TRANTER <TRANTER@POBOX.COM>
30 REM 13 MAY 2012
35 DIM N$(20), C$(20), T$(20), S$(12), A$(3)
40 GOSUB 10000 : REM CLEAR SCREEN
50 PRINT "           YOUR HOROSCOPE"
60 PRINT "           --------------"
70 PRINT ""
80 PRINT "THE PROGRAM GENERATES A PERSONAL"
90 PRINT "HOROSCOPE. I NEED SOME INFORMATION"
100 PRINT "ABOUT YOUR DATE AND LOCATION OF BIRTH."
110 PRINT "I WILL THEN GENERATE AN ANALYSIS BASED"
120 PRINT "ON YOUR ASTROLOGICAL DATA."
130 PRINT ""
140 REM GET INPUT FROM USER
150 INPUT "WHAT IS YOUR FIRST NAME", N$
160 INPUT "YOUR YEAR OF BIRTH (E.G. 1980)", Y
170 IF Y < 1900 OR Y > 2020 THEN 160
180 INPUT "YOUR MONTH OF BIRTH (1=JAN)", M
190 IF M < 1 OR M >12 THEN 180
200 INPUT "DAY OF THE MONTH (1-31)", D
210 IF D < 1 OR D > 31 THEN 200
220 INPUT "COUNTRY OF BIRTH", C$
230 INPUT "IN WHAT CITY", T$
240 PRINT "CALCULATING HOROSCOPE...PLEASE WAIT."
250 FOR I = 1 TO 3000 : NEXT I
260 PRINT
270 GOSUB 11000
280 GOSUB 10000
290 PRINT N$;", YOUR ASTROLOGICAL SIGN IS:"
300 PRINT S$;"."
310 PRINT ""
320 PRINT "HERE IS MY ANALYSIS:"
330 PRINT ""
340 L1 = 12010 + RND(35) * 10
350 L2 = 12010 + RND(35) * 10
360 IF L2 = L1 THEN 350
370 L3 = 12010 + RND(35) * 10
380 IF L3 = L1 THEN 370
390 IF L3 = L2 THEN 370
400 GOSUB L1
410 PRINT
420 GOSUB L2
430 PRINT
440 GOSUB L3
450 PRINT ""
460 INPUT "GENERATE ANOTHER HOROSCOPE (Y/N)", A$
470 IF A$ ="Y" THEN 40
480 IF A$ = "N" THEN END
490 GOTO 460
10000 REM CLEAR SCREEN
10010 FOR I = 1 TO 24
10020 PRINT
10030 NEXT I
10040 RETURN
11000 REM CALCULATE SIGN BASED ON BIRTH DATE
11010 IF M = 1 AND D <= 19 THEN S$ = "CAPRICORN"
11020 IF M = 1 AND D >= 20 THEN S$ = "AQUARIUS"
11030 IF M = 2 AND D <= 18 THEN S$ = "AQUARIUS"
11040 IF M = 2 AND D >= 19 THEN S$ = "PISCES"
11050 IF M = 3 AND D <= 20 THEN S$ = "PISCES"
11060 IF M = 3 AND D >= 21 THEN S$ = "ARIES"
11070 IF M = 4 AND D <= 19 THEN S$ = "ARIES"
11080 IF M = 4 AND D >= 20 THEN S$ = "TAURUS"
11090 IF M = 5 AND D <= 20 THEN S$ = "TAURUS"
11100 IF M = 5 AND D >= 22 THEN S$ = "GEMINI"
11110 IF M = 6 AND D <= 20 THEN S$ = "GEMINI"
11120 IF M = 6 AND D >= 21 THEN S$ = "CANCER"
11130 IF M = 7 AND D <= 22 THEN S$ = "CANCER"
11140 IF M = 7 AND D >= 21 THEN S$ = "LEO"
11150 IF M = 8 AND D <= 22 THEN S$ = "LEO"
11160 IF M = 8 AND D >= 23 THEN S$ = "VIRGO"
11170 IF M = 9 AND D <= 22 THEN S$ = "VIRGO"
11180 IF M = 9 AND D >= 23 THEN S$ = "LIBRA"
11190 IF M = 10 AND D <= 22 THEN S$ = "LIBRA"
11200 IF M = 10 AND D >= 23 THEN S$ = "SCORPIO"
11210 IF M = 11 AND D <= 21 THEN S$ = "SCORPIO"
11220 IF M = 11 AND D >= 22 THEN S$ = "SAGITTARIUS"
11230 IF M = 12 AND D <= 21 THEN S$ = "SAGITTARIUS"
11240 IF M = 12 AND D >= 22 THEN S$ = "CAPRICORN"
11250 RETURN
12000 REM PRINT ANALYSIS
12010 PRINT "AT TIMES YOU ARE EXTROVERTED, AFFABLE," : PRINT "SOCIABLE, WHILE AT OTHER TIMES YOU ARE"
12015 PRINT "INTROVERTED, WARY, RESERVED." : RETURN
12020 PRINT "AT TIMES YOU HAVE SERIOUS DOUBTS AS TO" : PRINT "WHETHER YOU HAVE MADE THE RIGHT"
12025 PRINT "DECISION OR DONE THE RIGHT THING." : RETURN
12030 PRINT "DISCIPLINED AND SELF-CONTROLLED OUTSIDE" : PRINT "YOU TEND TO BE WORRISOME AND INSECURE"
12035 PRINT "INSIDE." : RETURN
12040 PRINT "PEOPLE OFTEN TELL YOU THAT YOU REMIND" : PRINT "THEM OF SOMEONE ELSE." : RETURN
12050 PRINT "SECURITY IS ONE OF YOUR MAJOR GOALS IN" : PRINT "LIFE." : RETURN
12060 PRINT "SOME OF YOUR ASPIRATIONS TEND TO BE" : PRINT "PRETTY UNREALISTIC." : RETURN
12070 PRINT "THE BIRTHDAY OF A FRIEND OR RELATIVE" : PRINT "IS APPROACHING." : RETURN
12080 PRINT "YOU RECENTLY HAD A PROBLEM WITH YOUR" : PRINT "AUTOMOBILE OR THAT OF A FAMILY MEMBER." : RETURN
12090 PRINT "WHILE YOU HAVE SOME PERSONALITY" : PRINT "WEAKNESSES, YOU ARE GENERALLY ABLE TO"
12095 PRINT "COMPENSATE FOR THEM." : RETURN
12100 PRINT "YOU ARE SOMETIMES INSECURE, ESPECIALLY" : PRINT "WITH PEOPLE YOU DON'T KNOW VERY WELL." : RETURN
12110 PRINT "YOU ARE SOMETIMES UNCOMFORTABLE IN" : PRINT "FRONT OF LARGE GROUPS OF PEOPLE." : RETURN
12120 PRINT "YOU DON'T ALWAYS LISTEN TO YOUR" : PRINT "SIGNIFICANT OTHER AS MUCH AS YOU SHOULD." : RETURN
12130 PRINT "YOU DON'T ALWAYS STOP TO ENJOY THE" : PRINT "SIMPLE THINGS IN LIFE." : RETURN
12140 PRINT "YOU DON'T ALWAYS TAKE AS MUCH CARE OF" : PRINT "YOURSELF AS YOU SHOULD." : RETURN
12150 PRINT "YOU DON'T LIKE TIDYING UP." : RETURN
12160 PRINT "YOU DON'T LOVE YOURSELF ENOUGH." : RETURN
12170 PRINT "YOU HAD AN ACCIDENT WHEN YOU WERE A" : PRINT "CHILD INVOLVING WATER." : RETURN
12180 PRINT "YOU HAVE A BOX OF OLD UNSORTED" : PRINT "PHOTOGRAPHS IN YOUR HOUSE." : RETURN
12190 PRINT "YOU HAVE A GREAT DEAL OF UNUSED" : PRINT "CAPACITY WHICH YOU HAVE NOT TURNED"
12195 PRINT "TO YOUR ADVANTAGE." : RETURN
12200 PRINT "YOU HAVE A GREAT NEED FOR OTHER PEOPLE" : PRINT "TO LIKE AND ADMIRE YOU." : RETURN
12210 PRINT "YOU HAVE A TENDENCY TO BE CRITICAL OF" : PRINT "YOURSELF." : RETURN
12220 PRINT "YOU HAVE FOUND IT UNWISE TO BE TOO" : PRINT "FRANK IN REVEALING YOURSELF TO OTHERS." : RETURN
12230 PRINT "YOU ONCE SAID SOMETHING MEAN TO A" : PRINT "PERSON THAT YOU LATER REGRETTED" : PRINT "SAYING." : RETURN
12240 PRINT "YOU PREFER A CERTAIN AMOUNT OF CHANGE" : PRINT "AND VARIETY AND BECOME DISSATISFIED"
12245 PRINT "WHEN HEMMED IN BY RESTRICTIONS AND" : PRINT "LIMITATIONS." : RETURN
12250 PRINT "YOU PRIDE YOURSELF AS AN INDEPENDENT" : PRINT "THINKER AND DO NOT ACCEPT OTHERS'"
12255 PRINT "STATEMENTS WITHOUT SATISFACTORY PROOF." : RETURN
12260 PRINT "YOU RECENTLY HAD AN UNEXPECTED EXPENSE" : PRINT "YOU NEEDED TO PAY." : RETURN
12270 PRINT "YOU SOMETIMES ASK OTHERS FOR ADVICE" : PRINT "WHEN YOU KNOW IN YOUR HEART WHAT THE"
12275 PRINT "CORRECT COURSE OF ACTION IS." : RETURN
12280 PRINT "YOU SOMETIMES TEND TO GOSSIP TOO MUCH." : RETURN
12290 PRINT "YOU WISH THAT YOUR FINANCES WERE MORE" : PRINT "SECURE." : RETURN
12300 PRINT "YOU WORK HARDER THAN MOST PEOPLE BUT" : PRINT "DON'T OFTEN GET THE RECOGNITION YOU"
12305 PRINT "FEEL YOU DESERVE." : RETURN
12310 PRINT "YOU WORRY ABOUT YOUR HEALTH AS YOU GET" : PRINT "OLDER." : RETURN
12320 PRINT "YOU'RE HAVING PROBLEMS WITH A FRIEND" : PRINT "OR RELATIVE." : RETURN
12330 PRINT "YOUR SEXUAL ADJUSTMENT HAS PRESENTED" : PRINT "PROBLEMS FOR YOU." : RETURN
12340 PRINT "YOUR WORK IS SOMETIMES BORING WHILE" : PRINT "AT OTHER TIMES YOU FIND IT VERY" : PRINT "CHALLENGING." : RETURN
12350 PRINT "YOU ARE CONCERNED ABOUT THE HEALTH OF" : PRINT "AN AGING RELATIVE." : RETURN
13000 END
RUN
